apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        applicationId "com.redditgifts.mobile"

        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk

        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        testInstrumentationRunner "com.redditgifts.mobile.mocks.MockTestRunner"
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'redditgifts'
            keyAlias 'redditgifts'
            keyPassword 'redditgifts'
        }
        if (file('redditgifts.keystore').exists()) {
            upload {
                storeFile file('redditgifts.keystore')
                storePassword propOrEmpty('REDDITGIFTS_UPLOAD_STORE_PASSWORD')
                keyAlias 'redditgifts'
                keyPassword propOrEmpty('REDDITGIFTS_UPLOAD_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        release {
            if (file('redditgifts.keystore').exists()) {
                signingConfig signingConfigs.upload
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    implementation "androidx.appcompat:appcompat:${versions.androidX}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"

    implementation "androidx.core:core-ktx:1.0.1"

    implementation "io.reactivex.rxjava2:rxjava:2.2.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"

    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.4.0"
    implementation "com.squareup.retrofit2:converter-gson:2.4.0"

    implementation "com.squareup.okhttp3:okhttp:3.11.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"

    implementation "com.squareup.picasso:picasso:2.71828"

    implementation "com.google.dagger:dagger:${versions.dagger}"
    kapt "com.google.dagger:dagger-compiler:${versions.dagger}"

    implementation "org.jsoup:jsoup:1.11.3"

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }

    implementation "com.google.firebase:firebase-core:16.0.6"

    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.23.0"
    androidTestImplementation "androidx.test:runner:${versions.atsl}"
    androidTestImplementation "tools.fastlane:screengrab:1.2.0"
    androidTestImplementation "com.jraska:falcon:2.1.1"

}

apply plugin: 'com.google.gms.google-services'