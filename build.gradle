buildscript {
    ext.buildConfig = [
            'compileSdk': 28,
            'minSdk': 21,
            'targetSdk': 28,

            'version': [
                    'major': 1,
                    'minor': 4,
                    'patch': 0,
                    'build': 0,
            ],
    ]
    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext.versions = [
            'kotlin': '1.3.11',
            'androidX': '1.1.0-alpha01',
            'material': '1.1.0-alpha02',
            'okhttp': '3.11.0',
            'retrofit': '2.4.0',
            'atsl': '1.1.0-alpha3',
            'dagger': '2.17',
    ]

    repositories {
        maven { url 'https://maven.fabric.io/public' }
        google()
        jcenter()
    }

    dependencies {
        //noinspection GradleDependency
        classpath 'com.android.tools.build:gradle:3.3.0-rc03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "io.fabric.tools:gradle:1.27.0"
        classpath "com.google.gms:google-services:4.2.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }
    }
}

@SuppressWarnings("GroovyUnusedDeclaration")
def propOrEmpty(String name) {
    return hasProperty(name) ? getProperty(name) : ''
}
